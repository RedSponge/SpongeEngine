plugins { id 'org.beryx.runtime' version '1.8.4' }
apply plugin: 'application'

sourceSets.main.resources.srcDirs += [ rootProject.file('assets').path ]
mainClassName = 'com.redsponge.sponge.lwjgl3.Lwjgl3Launcher'
eclipse.project.name = appName + '-lwjgl3'
sourceCompatibility = 8.0

def osName = System.getProperty('os.name').toLowerCase(Locale.ROOT)

dependencies {
	implementation project(':core')
	implementation "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
	implementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
}

import org.gradle.internal.os.OperatingSystem

run {
	workingDir = rootProject.file('assets').path
	setIgnoreExitValue(true)
	
	if (OperatingSystem.current() == OperatingSystem.MAC_OS) {
		// Required to run LWJGL3 Java apps on MacOS
		jvmArgs += "-XstartOnFirstThread"
	}
}


jar {
	archiveFileName = "${appName}-${archiveVersion.get()}.jar"
	from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
	manifest {
		attributes 'Main-Class': project.mainClassName
	}
	destinationDirectory = file("$buildDir/lib")
}

runtime {
	options = ['--strip-debug',
			   '--compress', '2',
			   '--no-header-files',
			   '--no-man-pages',
			   '--strip-native-commands',
			   '--vm', 'server']
	modules = ['java.base' ,
			   'java.desktop',
			   'jdk.unsupported']
	distDir = file(buildDir)

	jpackage {
		//jpackageHome = '/usr/lib/jvm/open-jdk'
		mainJar = jar.archiveFileName.get()
		if (osName.contains('windows')) {
			imageOptions = ["--icon", file("../icons/icon.ico")]
		} else if (osName.contains('linux')) {
			imageOptions = ["--icon", file("../icons/icon.png")]
		} else if (osName.contains('mac')) {
			imageOptions = ["--icon", file("../icons/icon.icns")]
		}
	}
}

jpackageImage.dependsOn jar
